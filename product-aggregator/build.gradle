plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.protobuf' version '0.9.4'
}


dependencyManagement {
    imports {
        mavenBom("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:2.14.0")
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation("org.springframework.grpc:spring-grpc-client-spring-boot-starter:0.5.0")

    // security
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    //   Resilience4j
    implementation group: 'io.github.resilience4j', name: 'resilience4j-spring-boot3', version: '2.3.0'
    // official doc sample : https://github.com/resilience4j/resilience4j-spring-boot3-demo/blob/master/build.gradle.kts
    implementation("org.springframework.boot:spring-boot-starter-aop")
    //  Optional, only required when you want to use the Decorators class
    implementation group: 'io.github.resilience4j', name: 'resilience4j-all', version: '2.3.0'
    implementation group: 'io.github.resilience4j', name: 'resilience4j-reactor', version: '2.3.0'

    // Observability libraries
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.opentelemetry.instrumentation:opentelemetry-spring-boot-starter'
    // log trace and span id
    runtimeOnly group: 'io.opentelemetry.instrumentation', name: 'opentelemetry-logback-mdc-1.0', version: '2.14.0-alpha'
    // Metrics
    implementation 'io.micrometer:micrometer-core'
    runtimeOnly 'io.micrometer:micrometer-registry-otlp'
    runtimeOnly group: 'io.opentelemetry.instrumentation', name: 'opentelemetry-micrometer-1.5', version: '2.14.0-alpha'
    // needed to export resilient4j metrics
    implementation group: 'io.github.resilience4j', name: 'resilience4j-micrometer', version: '2.3.0'

    // grpc instrumentation
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
    implementation("io.opentelemetry.instrumentation:opentelemetry-grpc-1.6:2.14.0-alpha")
}
group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}



tasks.named('test') {
    useJUnitPlatform()
}



protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.24.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.59.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}
